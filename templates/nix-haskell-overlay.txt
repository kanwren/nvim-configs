self: super:

let
  # If using niv
  sources = import ./sources.nix;

  inherit (super) lib;
  hlib = super.haskell.lib;
  clean = super.nix-gitignore.gitignoreSource [];

  ghcOverride = input: ovl: input.override (old: {
    overrides = lib.composeExtensions (old.overrides or (_: _: { })) ovl;
  });

  fixGhcWithHoogle = input: ghcOverride input (hself: hsuper: {
    # Compose the selector with a null filter to fix error on null packages
    ghcWithHoogle = selector:
      hsuper.ghcWithHoogle (ps: builtins.filter (x: x != null) (selector ps));
    ghc = hsuper.ghc // { withHoogle = hself.ghcWithHoogle; };
  });

  # Package overrides
  packageOverlay = hself: hsuper: { };

  # Result packages
  mainOverlay = hself: hsuper: {
    foobar = hsuper.callCabal2nix "foobar" (clean ../.) { };
  };

  composeOverlays = lib.foldl' lib.composeExtensions (_: _: { });
  haskellOverlay = composeOverlays [ mainOverlay packageOverlay ];

in {
  niv = (import sources.niv {}).niv;

  haskell = super.haskell // {
    packages = super.haskell.packages // {
      ghc8101 = fixGhcWithHoogle
        (ghcOverride super.haskell.packages.ghc8101 haskellOverlay);
    };
  };
}

